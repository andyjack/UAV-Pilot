#!/usr/bin/perl
use v5.14;
use warnings;
use IO::Socket::INET;
use UAV::Pilot;
use UAV::Pilot::Driver::ARDrone;
use UAV::Pilot::Driver::ARDrone::Video;
use UAV::Pilot::Driver::ARDrone::Video::Mock;
use UAV::Pilot::Control::ARDrone::Video::FileDump;
use AnyEvent;
use Getopt::Long;

my $HOST     = '192.168.1.1';
my $FILE_OUT = '';
my $FILE_IN  = '';
Getopt::Long::GetOptions(
    'out=s' => \$FILE_OUT,
    'in=s'  => \$FILE_IN,
);

$SIG{'INT'} = 'cleanup';


my $ardrone = UAV::Pilot::Driver::ARDrone->new({
    host => $HOST,
});

my $fh = undef;
if( $FILE_OUT eq '' ) {
    $fh = \*STDOUT;
}
else {
    open( $fh, '>', $FILE_OUT )
        or die "Can't open $FILE_OUT for writing: $!\n";
}
my $control_video = UAV::Pilot::Control::ARDrone::Video::FileDump->new({
    fh => $fh,
});

my $cv = AnyEvent->condvar;
my %video_args = (
        handler => $control_video,
        condvar => $cv,
        driver  => $ardrone,
);

my $driver_video = $FILE_IN
    ? UAV::Pilot::Driver::ARDrone::Video::Mock->new({
        %video_args,
        file => $FILE_IN,
    })
    : UAV::Pilot::Driver::ARDrone::Video->new( \%video_args );
$driver_video->init_event_loop;
$cv->recv;


END { cleanup() }


sub cleanup
{
     warn "Frames processed: " . $driver_video->frames_processed . "\n"
        if defined $driver_video;
     close $fh if defined $fh;
     exit;
}
